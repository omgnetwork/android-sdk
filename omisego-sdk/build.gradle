apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'checkstyle'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

publish {
    userOrg = 'omise-go'
    groupId = 'co.omisego'
    artifactId = 'omisego-sdk'
    publishVersion = versionName
    desc = 'The OmiseGO Android SDK allows developers to easily interact with a node of the OmiseGO eWallet.'
    website = 'https://github.com/omisego/android-sdk'
}

checkstyle {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    include '**/*.kt'
    exclude '**/gen/**'

    // empty classpath
    classpath = files()
}

task runUnitTests(dependsOn: [':app:testDebugUnitTest']) {
    description 'Run all unit tests'
}

tasks.withType(Javadoc) {
    enabled = false
}

dependencies {
    // Core dependencies
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Used for the QRCode generator and the QRCode scanner.
    api 'com.google.zxing:core:3.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:support-v4:27.1.0'

    // Testing dependencies
    testImplementation 'org.json:json:20160810'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation 'org.amshove.kluent:kluent-android:1.30'
    testImplementation("com.nhaarman:mockito-kotlin:1.5.0", {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    })
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation 'com.squareup.retrofit2:retrofit-mock:2.3.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
}

repositories {
    mavenCentral()
}

apply plugin: 'kotlin-android-extensions'
